/* -*- java -*-

This software is OSI Certified Open Source Software.
OSI Certified is a certification mark of the Open Source Initiative.

The license (Mozilla version 1.0) can be read at the MMBase site.
See http://www.MMBase.org/license

*/
/**
 * DateParsed inspired by at(1) syntax, and original mm:time.
 * This file is parsed by <a href="https://javacc.dev.java.net/">javacc</a>.
 * Implemented many years ago for MMBase. Changed for modern times (Calendar -> ZonedDateTime)
 *
 * @author Michiel Meeuwisssen
 * @since MMBase-1-8
 * @since 0.19
 */

options {
    LOOKAHEAD = 2;
    STATIC = false;
    FORCE_LA_CHECK = false;
    IGNORE_CASE = true;
    CHOICE_AMBIGUITY_CHECK = 2;
    GENERATE_GENERICS = true;
    GENERATE_STRING_BUILDER	= true;
    JAVA_TEMPLATE_TYPE = "modern";
    UNICODE_INPUT = true;
}

PARSER_BEGIN(DateParser)

package org.meeuw.time.parser;


import java.time.*;
import java.time.temporal.*;
import java.time.chrono.*;
import java.io.*;
import java.util.*;
import java.util.function.Supplier;
import org.meeuw.time.Range;


class DateParser implements Supplier<ZonedDateTime> {
    private boolean dynamic = true;
    private ZonedDateTime prev = null;
    private int amount = 0;
    private int factor = 1;

    private TemporalUnit unit = null;
    private int monthorday = -1;
    private int week = -1;
    private boolean positive = true;

    private String eventName = null;

    private ZonedDateTime cal = null;

    private DynamicDateTime dynamicDateTime;

    protected void setDynamicDateTime(DynamicDateTime dynamicDateTime) {
        this.dynamicDateTime = dynamicDateTime;
        this.cal = getInstance();
    }

    public ZonedDateTime get()  {
        return cal;
    }
    public boolean dynamic()  {
        return dynamic;
    }

    protected void beginOfMinute() {
        cal = cal.withSecond(0).withNano(0);
    }

    protected void beginOfHour() {
        cal = cal.withMinute(0);
        beginOfMinute();
    }

    protected void beginOfDay() {
        cal = cal.withHour(0);
        beginOfHour();
    }

    protected void beginOfWeek() {
        beginOfDay();
        setDayOfWeek(dynamicDateTime.getFirstDayOfWeek());
    }

    protected void beginOfMonth() {
        cal = cal.withDayOfMonth(1);
        beginOfDay();
    }
    protected void beginOfYear() {
        setMonth(Month.JANUARY);
        beginOfMonth();
    }
    protected void beginOfCentury(boolean pedantic) {
        beginOfYear();
        int year = cal.getYear();
        year -= (year % 100);
        cal = cal.withYear(year + (pedantic ? 1 : 0));
    }
    protected void beginOfEra() {
        beginOfYear();
        cal = cal.withYear(1970);
    }

    protected ZonedDateTime getInstance() {
        return dynamicDateTime.getInstance();
    }
    protected void setDayOfWeek(DayOfWeek day) {
        cal = cal.with(TemporalAdjusters.nextOrSame(day));
    }
    protected void setMonth(Month month) {
        cal = cal.withMonth(month.getValue());
    }
}

PARSER_END(DateParser)

SKIP : {" " | "\t" | "\n" | "\r" }

TOKEN :
{
 <NEXT: "next" >
| <PREVIOUS: "previous" >
| <THIS: "this" >
| <TO: "to" >
| <POSITIVE: ["0"-"9"] (["0"-"9"])* >
| <THOUSANDS: "." ["0"-"9"]["0"-"9"]["0"-"9"] >
| <TIMEZONE: "TZ" ([" ", "\n", "\t", "\r"])* ["A"-"Z"] (["A"-"Z","a"-"z", "/", "_"])* (["-", "+"] <POSITIVE> (":" <POSITIVE>)?)?>
 | <EVENT: "\"" (["a"-"z", " "])+ "\"" >


}


void start() : {}
{
    (time_zone())? (date() (["T"] time())?)? (increment() | decrement())* (time_zone())? <EOF>
      // T: only for compatibility with ISO_8601
}


void time() : {}
{
    hour_number() ":" minute_number() (":" second_number() (millisecond_number())?)?
    | "noon"     { beginOfHour(); cal = cal.withHour(12); }
    | "midnight" { beginOfHour(); cal = cal.withHour(0); }
    | "teatime"  { beginOfHour(); cal = cal.withHour(16); }
    | hour_number() "oclock" { cal = cal.withMinute(0);}
}

void date() :  {}
{
    LOOKAHEAD(6)

    year_number() "-" month_or_day_or_week_number() ("-" day_of_month_or_week_number())? {
         if (monthorday > 0) cal = cal.withDayOfYear(monthorday);
         if (week >= 0) {
             //cal.withWeek(Calendar.WEEK_OF_YEAR, week);
             //cal.set(Calendar.DAY_OF_WEEK, 2);
             week = -1;
         }
         beginOfDay();
         dynamic = false;
    }
    | day_of_week() {beginOfDay();}
    | month_of_year() {beginOfMonth();}
    | "now"      { cal = getInstance(); }
    | seconds()  {  }
    | <TO> to_period() {}
    | "tomorrow" {
        cal = getInstance();
        cal = cal.plusDays(1);
        beginOfDay();
        }
    | "yesterday" { cal = getInstance();
          cal = cal.minus(Duration.ofDays(1));
          beginOfDay(); }
    | "yestermonth" { cal = getInstance();
            cal = cal.minusMonths(1);
            beginOfMonth(); }
    | "yesteryear" { cal = getInstance();
     cal = cal.minusYears(1); beginOfYear(); }
    | "duration" {
         beginOfYear();
         dynamicDateTime.setZoneId(ZoneId.of("UTC"));
         cal = dynamicDateTime.resetToEra();
        }
    | "borreltijd"  {
           beginOfHour();
           cal = cal.withHour(17);
           setDayOfWeek(DayOfWeek.FRIDAY);
    }
    | event() {
        cal = dynamicDateTime.getForEvent(cal, eventName);
   }
}

void event() : {   }
{
    <EVENT> { eventName = token.image.substring(1, token.image.length() - 1).toLowerCase(); }

}


void time_zone() : {}
{
 <TIMEZONE> {
    cal = cal.withZoneSameLocal(ZoneId.of(token.toString().substring(2).trim()));}
  |  "Z" {
      cal.withZoneSameLocal(ZoneId.of("UTC"));
} //  For compatibility ISO_8601 (with trailing 'Z')
}

void unary() : {}
{
    "-" { positive = false; }
|   "+" { positive = true; }

}
void integer() : {}
{
    unary() <POSITIVE>
|  <POSITIVE>
}

void seconds() : {}
{
    integer() {

      cal = dynamicDateTime.resetToEra();
      cal = cal.plusNanos(1_000_000 * ((positive ? 1 : -1 ) * Long.parseLong(token.toString()) * 1000));
      dynamic = false; positive = true;
    }
}
void year_number() : { }
{
    integer()  {
          cal = cal.withYear((positive ? 1 : -1) * Integer.parseInt(token.toString()));
          positive = true;
    }
}
void week_number() : { }
{
    <POSITIVE>  {
        cal = cal.with(IsoFields.WEEK_OF_WEEK_BASED_YEAR, Integer.parseInt(token.toString()));
    }
}

void inc_number() : { }
{
    <POSITIVE> { amount = Integer.parseInt(token.toString()); }
}
void month_or_day_or_week_number() : { }
{
    <POSITIVE> { monthorday = Integer.parseInt(token.toString());  week = -1; }
    |  "W" <POSITIVE> { monthorday = -1; week = Integer.parseInt(token.toString());  }
}
void day_of_month_or_week_number() : { }
{
    <POSITIVE> {
        if (monthorday > 0) {
            cal = cal.withMonth(monthorday);
            monthorday = -1;
            cal = cal.withDayOfMonth(Integer.parseInt(token.toString()));
        } else {
            cal = cal.with(IsoFields.WEEK_OF_WEEK_BASED_YEAR, week);
            week = -1;
            int day = (Integer.parseInt(token.toString()) + 1) % 7; if (day == 0) day = 7;
            setDayOfWeek(DayOfWeek.of(day));
        }
    }
}
void day_of_year_number() : { }
{
    <POSITIVE> {
       cal = cal.withDayOfYear(Integer.parseInt(token.toString()));
    }
}
void day_of_week_number() : { }
{
    <POSITIVE> {

       setDayOfWeek(DayOfWeek.of(Integer.parseInt(token.toString())));
    }
}

void hour_number() : { }
{
    <POSITIVE> {
        int parsed = Integer.parseInt(token.toString());
        while(parsed > 23) {
            parsed -= 24;
            cal = cal.plusDays(1);
        }
        while (parsed < 0) {
            parsed += 24;
            cal = cal.minusDays(1);
        }
        cal = cal.withHour(parsed);
     }
}
void minute_number() : { }
{
    <POSITIVE> {
        cal = cal.withMinute(Integer.parseInt(token.toString()));
        cal = cal.withSecond(0);
        cal = cal.withNano(0);
    }
}
void second_number() : { }
{
    <POSITIVE> {
       cal = cal.withSecond(Integer.parseInt(token.toString()));
       cal = cal.withNano(0);
    }
}
void millisecond_number() : { }
{
    <THOUSANDS> {
         cal = cal.withNano(1_000_000 * Integer.parseInt(token.toString().substring(1)));
    }
}


void increment() : {}
{
  "+"  inc_number() inc_period() {
       cal = cal.plus(amount * factor, unit); factor = 1;
      }
   | <NEXT> inc_period()  { cal = cal.plus(1 * factor, unit); factor = 1;}
   | <PREVIOUS> inc_period()  {  cal = cal.minus(- 1 * factor, unit); factor = 1;}
   | next() day_of_week() { beginOfDay(); if (cal.isBefore(prev)) {cal = cal.plusDays(7); } }
   | previous() day_of_week() { beginOfDay(); if (cal.isAfter(prev)) {cal = cal.minusDays(7); } }
  | <THIS> day_of_week() { beginOfDay(); }
   | next() month_of_year() { beginOfMonth(); if (cal.isBefore(prev)) {cal = cal.plusYears(1); } }
   | previous() month_of_year() { beginOfMonth(); if (cal.isAfter(prev)) {cal = cal.minusYears(1); } }
   | <THIS> month_of_year() { beginOfMonth(); }
   | <THIS> to_period()
   | <THIS> to_event()
   | "ish"  {  cal = cal.plusNanos(1_000_000 * ((int) (dynamicDateTime.random.nextGaussian() * 1200000.0)/* 20 min */ + 300000 /* 5 minutes late on average*/)); dynamic = true;}
}
void decrement() : {}
{
    "-"  inc_number() inc_period() {
        cal = cal.minus(amount * factor, unit);
        factor = 1;
      }
}
void to_period() : {
}
{
    this_period()
}

void to_event() : {
   }
{
   <EVENT> {eventName = token.image.substring(1, token.image.length() - 1).toLowerCase();
       cal = dynamicDateTime.getForEvent(cal, eventName);

      }
}
void inc_period() : {}
{
  "second" { unit = ChronoUnit.SECONDS; }
  | "seconds" { unit = ChronoUnit.SECONDS; }
 | "minute" { unit = ChronoUnit.MINUTES;  }
 | "minutes" { unit = ChronoUnit.MINUTES;  }
 | "hour"   { unit = ChronoUnit.HOURS; }
 | "hours"   { unit = ChronoUnit.HOURS; }
 | "day"    { unit = ChronoUnit.DAYS;}
 | "days"    { unit = ChronoUnit.DAYS;}
 | "week"   { unit = ChronoUnit.WEEKS; }
 | "weeks"   { unit = ChronoUnit.WEEKS; }
  | "month"  { unit = ChronoUnit.MONTHS; }
  | "months"  { unit = ChronoUnit.MONTHS; }
  | "year" {   unit = ChronoUnit.YEARS; }
  | "years" {   unit = ChronoUnit.YEARS; }
  | "century" {   unit = ChronoUnit.YEARS; factor =  100;}
  | "centuries" {   unit = ChronoUnit.YEARS; factor =  100;}
 | "millisecond" { unit = ChronoUnit.MILLIS; factor = 1; }
 | "milliseconds" { unit = ChronoUnit.MILLIS; factor = 1; }
}

void this_period() : {}  {
  "second"  { cal = cal.withNano(0); }
  | "minute"  { beginOfMinute(); }
  | "hour"  { beginOfHour(); }
  | "day"  { beginOfDay(); }
  | "week"  { beginOfWeek(); }
  | "month" { beginOfMonth(); }
  |  "year" { beginOfYear(); }
  | "century" { beginOfCentury(false); }
  | "century_pedantic" { beginOfCentury(true); }
  | "era" { beginOfEra(); }
}
void day_of_week() : {
    // If you don't do this. '2005-01-01 this monday' parses to something odd. Which I don't quite understand.
    //cal.getTime();
}
{
  "sunday" { setDayOfWeek(DayOfWeek.SUNDAY);}
  | "monday" { setDayOfWeek(DayOfWeek.MONDAY);}
| "tuesday" { setDayOfWeek(DayOfWeek.TUESDAY); }
| "wednesday" { setDayOfWeek(DayOfWeek.WEDNESDAY); }
| "thursday" { setDayOfWeek(DayOfWeek.THURSDAY); }
| "friday" {setDayOfWeek(DayOfWeek.FRIDAY);}
| "saturday" { setDayOfWeek(DayOfWeek.SATURDAY); }
}
void month_of_year() : {}
{
  "january" { setMonth(Month.JANUARY); }
| "february" { setMonth(Month.FEBRUARY); }
| "march" { setMonth(Month.MARCH); }
| "april" { setMonth(Month.APRIL); }
| "may" { setMonth(Month.MAY); }
| "june" { setMonth(Month.JUNE); }
| "july" { setMonth(Month.JULY); }
| "august" { setMonth(Month.AUGUST); }
| "september" { setMonth(Month.SEPTEMBER); }
| "october" { setMonth(Month.OCTOBER); }
| "november" { setMonth(Month.NOVEMBER); }
| "december" { setMonth(Month.DECEMBER); }
}

void next() : {}
{
    <NEXT> { prev = cal; }
}
void previous() : {}
{
    <PREVIOUS> { prev = cal; }
}
